plugins {
    id 'java'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    myTestClasses {
        java {}
    }
    otherTestClasses {
        java {}
        compileClasspath += sourceSets.myTestClasses.output
    }
}

final String testJarsDir = "$buildDir/test-jars"

task myTestJar(type: Jar) {
    archiveFileName = 'my-tests.jar'
    from sourceSets.myTestClasses.output
    destinationDirectory.set file(testJarsDir)
}

task otherTestJar(type: Jar) {
    archiveFileName = 'other-tests.jar'
    from sourceSets.otherTestClasses.output
    destinationDirectory.set file(testJarsDir)
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.assertj:assertj-core:3.21.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    dependsOn('myTestJar', 'otherTestJar')
    useJUnitPlatform()
    systemProperty('tests.repo.dir', file('src/test/resources/jbuild/commands/repo').absolutePath)
    systemProperty('tests.test-classes.dir', testJarsDir)
    systemProperty('tests.my-test-classes.jar', "$testJarsDir/my-tests.jar")
    systemProperty('tests.other-test-classes.jar', "$testJarsDir/other-tests.jar")
    systemProperty('tests.real-jars.osgiaas-cli-api.jar', "$rootDir/src/test/real-jars/osgiaas-cli-api-0.7.jar")
}
